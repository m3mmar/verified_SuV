#ifndef GHOST_LISTS_H
#define GHOST_LISTS_H

predicate ghost_list<t>(int id; list<t> xs);
predicate ghost_list_member_handle<t>(int id, t x;);

lemma int create_ghost_list<t>();
    requires true;
    ensures ghost_list<t>(result, nil);

lemma void ghost_list_insert<t>(int id, list<t> xs0, list<t> xs1, t x);
    requires ghost_list<t>(id, append(xs0, xs1));
    ensures ghost_list<t>(id, append(xs0, cons(x, xs1))) &*& ghost_list_member_handle<t>(id, x);

lemma void ghost_list_match<t>();
    requires ghost_list<t>(?id, ?xs) &*& [?f]ghost_list_member_handle<t>(id, ?x);
    ensures ghost_list<t>(id, xs) &*& [f]ghost_list_member_handle<t>(id, x) &*& mem(x, xs) == true;

lemma void ghost_list_remove<t>(int id, list<t> xs0, list<t> xs1, t x);
    requires ghost_list<t>(id, append(xs0, cons(x, xs1))) &*& ghost_list_member_handle<t>(id, x);
    ensures ghost_list<t>(id, append(xs0, xs1));

lemma void mem_remove_eq_append<t>(t x, list<t> xs);
    requires mem(x, xs) == true;
    ensures exists<pair<list<t>, list<t> > >(pair(?xs1, ?xs2)) &*& xs == append(xs1, cons(x, xs2)) &*& remove(x, xs) == append(xs1, xs2);

#endif
